//
// FOnline: 2238
// Rotators
//
// mapper_utils.fos
//

#ifndef __MAPPER_UTILS__
#define __MAPPER_UTILS__

#include "_mapper_defines.fos"
#include "_mapper_macros.fos"


// Returns handle to active map
//
MapperMap@ GetActiveMap()
{
    array<MapperMap@> maps;
    int               cur = GetLoadedMaps(maps);
    if(cur == -1)
    {
        return null;
    }
    return maps[cur];
}

// Returns an offset between hexes in some direction, in screen pixels.
// There might be a better way to do it...
//
void HexOffset(uint8 dir, int& x, int& y)
{
    int x1 = 0,
        y1 = 0,
        x2 = 0,
        y2 = 0;

    uint16 frX = 2,
           frY = 2,
           toX = frX,
           toY = frY;

    MoveHexByDir(toX, toY, (dir % 6), 1);

    if(dir > 5)     // dir > 5 is interpreted as 2 hexes away, "intermediate" directions
    {
        MoveHexByDir(toX, toY, _TurnRight(dir, 1), 1);
    }

    GetHexPos(frX, frY, x1, y1);
    GetHexPos(toX, toY, x2, y2);

    x = x2 - x1;
    y = y2 - y1;
}

uint GetHexWidth()
{
    int x1 = 0, x2 = 0, y = 0;
    GetHexPos(0, 0, x1, y);
    GetHexPos(1, 0, x2, y);
    return x1 - x2;
}


// Returns current zoom level, in %. Not very precise :P
//
uint GetZoom()
{
    int x = 0;
    int y1 = 0;
    int y2 = 0;
    GetHexPos(0, 0, x, y1);
    GetHexPos(0, 9, x, y2);
    return (y2 - y1) * 100 / 108;
}

// "Patch"
//
bool GetMonitorHexSigh(int x, int y, uint16& hx, uint16& hy, bool ignoreInterface = false)
{
    int saveMouseX = __MouseX;
    int saveMouseY = __MouseY;
    __MouseX = x;
    __MouseY = y;
    bool result = GetMonitorHex(x, y, hx, hy, ignoreInterface);
    __MouseX = saveMouseX;
    __MouseY = saveMouseY;
    return result;
}
#endif // __MAPPER_UTILS__ //
