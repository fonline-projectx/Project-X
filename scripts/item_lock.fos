//
// FOnline: 2238
// Rotators
//
// item_lock.fos
//

// Lock that can be used on lockers to...lock them

#include "_macros.fos"

void item_init(Item& item, bool firstTime)
{
    item.SetEvent(ITEM_EVENT_USE, "_Use");
    item.LockerComplexity = Random(50, 100);
}

/**
 * Lock used on opened locker changes its lockerid and gives you key to it.
 */
bool _Use(Item& item, Critter& crit, Critter@ onCritter, Item@ onItem, Scenery@ onScenery)
{
    if(!valid(onItem) || onItem.GetType() != ITEM_TYPE_CONTAINER)
        return false;         // that does nothing

    // already locked
    if(_LockerIsClose(onItem))
        return false;

    uint lockId = Random(1, 65535);

    onItem.LockerId = lockId;
    onItem.LockerComplexity = item.LockerComplexity;

    // remove it
    if(item.GetCount() > 1)
        item.SetCount(item.GetCount() - 1);
    else
        DeleteItem(item);
    // give key
    Item@ key = crit.AddItem(PID_KEY, 1);
    key.LockerId = lockId;
    key.Update();
    // info
    crit.SayMsg(SAY_NETMSG, TEXTMSG_TEXT, 4400);
    return true;
}
