//
// FOnline: 2238
// Rotators
//
// map_slaverun.fos
//

// This is the map script for slaverun locations.
// See slaverun.fos for the list of all involved files.

#include "_macros.fos"

#include "slaverun_h.fos"
#include "utils_h.fos"

import void FillMap(Map@ map, uint& hostiles, uint& normals, uint& slavers, uint teamId) from "slaverun";

void map_init(Map& map, bool firstTime)
{
    map.SetEvent(MAP_EVENT_IN_CRITTER, "_OnInCritter");
    map.SetEvent(MAP_EVENT_OUT_CRITTER, "_OnOutCritter");
    map.SetData(MAP_DATA_SLAVERUN_INIT, UNITIALIZED);
    map.SetData(MAP_DATA_SLAVERUN_PLAYER, 0);
    map.SetData(MAP_DATA_SLAVERUN_COMBAT, UNITIALIZED);
}

void _OnInCritter(Map& map, Critter& player)
{
    if(!player.IsPlayer())
        return;
    map.GetLocation().AutoGarbage = true;
    GameVar@ loc = GetLocalVar(LVAR_slaverun_current_loc, player.Id);
    if(loc != map.GetLocation().Id)
        return;

    EraseTimeEvent(GetLvar(player, LVAR_slaverun_eventnum));
    map.SetData(MAP_DATA_SLAVERUN_PLAYER, player.Id);
    if(map.GetData(MAP_DATA_SLAVERUN_INIT) != INITIALIZED)       // should be always true TODO!!!!
    {
        map.SetData(MAP_DATA_SLAVERUN_INIT, INITIALIZED);
        uint     hostiles = 0;
        uint     normals = 0;
        uint     slavers = 0;

        GameVar@ status = GetLocalVar(LVAR_slaverun_status, player.Id);
        status = SLAVERUN_ATTEMPTED | SLAVERUN_ACTIVE;

        FillMap(map, hostiles, normals, slavers, GetGroupIndex(player));
        SetLvar(player, LVAR_slaverun_hostiles, hostiles);
        SetLvar(player, LVAR_slaverun_normals, normals);
        SetLvar(player, LVAR_slaverun_slavers, slavers);
        SetLvar(player, LVAR_slaverun_hostiles_killed, 0);
        SetLvar(player, LVAR_slaverun_normals_killed, 0);
        SetLvar(player, LVAR_slaverun_slavers_killed, 0);
    }
    player.Say(SAY_NETMSG, "You've entered the slave run area.");
}

void _OnOutCritter(Map& map, Critter& player)
{
    if(player.IsPlayer())
        player.UnsetKnownLoc(true, map.GetLocation().Id);
}
